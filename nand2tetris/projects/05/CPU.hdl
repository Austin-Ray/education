// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Setup A-instruction and C-instruction names for readability.
    Not(in=instruction[15], out=ains);
    Not(in=ains, out=cins);

    // Only use ALU output for C-instructions.
    And(a=cins, b=instruction[5], out=usealuout);
    Mux16(a=instruction, b=aluout, sel=usealuout, out=out1);

    // Load into A register if it's an A-instruction or ALU output.
    Or(a=ains, b=usealuout, out=loada);
    ARegister(in=out1, load=loada, out=aregout, out[0..14]=addressM);
    
    Mux16(a=aregout, b=inM, sel=instruction[12], out=out2);

    And(a=cins, b=instruction[4], out=loadd);
    DRegister(in=aluout, load=loadd, out=dregout);

    ALU(x=dregout, y=out2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluout, out=outM, zr=zero, ng=neg);

    // If it's a C instruction, save to M.
    And(a=cins, b=instruction[3], out=writeM);

    // Jump logic
    Or(a=zero, b=neg, out=zeroorneg);
    Not(in=zeroorneg, out=pos);

    And(a=instruction[0], b=pos, out=jgt);
    And(a=instruction[1], b=zero, out=jeq);
    And(a=instruction[2], b=neg, out=jlt);

    // By comparing these three functions, we check all possible JUMPs.
    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=jump);

    // Make sure we only jump with a C-instruction.
    // A-instructions contain bits in the J spots, which will execute incorrectly.
    And(a=cins, b=jump, out=loadpc);

    // If there's no JUMP, then increment.
    Not(in=loadpc, out=incpc);
    PC(in=aregout, load=loadpc, inc=incpc, reset=reset, out[0..14]=pc);
}
